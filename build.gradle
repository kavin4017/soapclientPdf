plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '21'

configurations {
    jaxb
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'wsdl4j:wsdl4j:1.6.3'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.1'
    jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.3'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    outputs.dir sourcesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)

            xjc(destdir: sourcesDir, package: 'com.example.client.wsdl') {
                arg(value: "-wsdl")
                schema(dir: "src/main/resources/wsdl", includes: "requests.wsdl")
            }
        }
    }
}

compileJava.dependsOn genJaxb

sourceSets {
    main {
        java {
            srcDirs += genJaxb.sourcesDir
        }
    }
}
